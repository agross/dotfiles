#!/usr/bin/env zsh

autoload -U colors && colors

function dnd::on() {
  shortcuts run dnd-on

  printf 'DND turned %bon%b\n' "$fg[green]" "$reset_color"
}

function dnd::off() {
  shortcuts run dnd-off

  printf 'DND turned %boff%b\n' "$fg[red]" "$reset_color"
}

(($+commands[m])) || {
  >&2 printf 'Need m executable from https://github.com/rgcr/m-cli\n'
  exit 1
}

# Pass any argument to turn off DND.
if [[ $# -gt 0 ]] ||
   [[ $(defaults read com.apple.controlcenter 'NSStatusItem Visible FocusModes') == 1 ]]; then
  killall caffeinate 2> /dev/null
  m volume unmute
  open -a Flux
  open -a DockMate
  osascript <<< 'tell application "PopClip" to set enabled to true'

  if brew autoupdate status | grep --quiet --invert-match running; then
    brew autoupdate start
  fi

  dnd::off
else
  # http://unix.stackexchange.com/a/148698/72946
  # http://stackoverflow.com/a/10408906/149264
  nohup caffeinate -u -t $((9 * 60 * 60)) < /dev/null > /dev/null 2>&1 &disown

  if ! scutil --dns | grep --quiet --fixed-strings home.therightstuff.de; then
    # Only mute while not at home.
    m volume mute
  fi

  killall Flux 2> /dev/null
  killall DockMate 2> /dev/null
  osascript <<< 'tell application "PopClip" to set enabled to false'

  if brew autoupdate status | grep --quiet running; then
    brew autoupdate stop
  fi

  dnd::on
fi
