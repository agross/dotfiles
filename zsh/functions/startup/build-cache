local -a functions
if [[ "$#" -eq 0 ]]; then
  functions=(is-mintty)
else
  functions=($@)
fi

# A global variable is needed since we cannot capture closures in zsh.
typeset -gA _function_cache

local fun
for fun in $functions; do
  # Call function.
  local result=''
  result="$($fun)"

  # Put exit status and output in cache.
  _function_cache[${fun}_exit_code]=$?
  _function_cache[${fun}_output]="$result"

  verbose Caching result of $fg[red]$fun$reset_color \
    with output $fg[green]\"${_function_cache[${fun}_output]}\"$reset_color \
    and exit code $fg[green]${_function_cache[${fun}_exit_code]}$reset_color

  # Redefine function to return result.
  function $fun () {
    local fun=$0

    local output="${_function_cache[${fun}_output]}"
    if [[ -n "$output" ]]; then
      echo $output
    fi

    return ${_function_cache[${fun}_exit_code]}
  }
done

# vim:set filetype=sh:
