autoload -U zgitinit
zgitinit

autoload -U zgit-ext-rebaseinfo

function {
  # Disable replacing the current directory path generated by %~ (see
  # _prompt::agross::cwd) with the variable name if the variable has the same
  # value as the absolute path to the current directory.
  unsetopt auto_name_dirs

  # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
  local user_host='%F{green}%n@%m%f'
  local cwd='%F{yellow}$(_prompt::agross::cwd)%f'
  local git_branch='%F{green}$(_prompt::agross::git_branch)%f'
  local git_status_symbols='%B%F{red}$(_prompt::agross::git_status)%f%b'
  local jobs='%(1j|%B%F{magenta} %j job%(2j|s|)%f%b|)'
  local git_or_exit_status='%(?|$(_prompt::agross::symbol)|%F{red}%K{white}%B%S↑%s%b%k%f)'

  # Start with newline.
  PROMPT=$'\n'
  PROMPT+="$user_host $cwd $git_branch$git_status_symbols$jobs%E"
  PROMPT+=$'\n'
  if (($+functions[iterm2_prompt_mark])) && \
     [[ "$ITERM_PROFILE" != 'zsh Trainings' ]]; then
    PROMPT+='%{$(iterm2_prompt_mark)%}'
  fi
  PROMPT+="$git_or_exit_status %E"

  RPROMPT=
}

function _prompt::agross::cwd() {
  printf '%%~'
}

function _prompt::agross::symbol {
  zgit_isgit && printf '±' && return
  printf '$'
}

function _prompt::agross::git_branch() {
  zgit_isgit || return
  zgit-ext-rebaseinfo

  local extra

  zgit_ingitdir && extra='GIT_DIR! '
  zgit_isbare   && extra='BARE! '

  printf "$extra@$(zgit_head)$zgit_info[rebase]"
}

function _prompt::agross::git_status() {
  zgit_inworktree || return

  zgit_isworktreeclean      || printf '*'
  zgit_hasuntracked         && printf '?'
  zgit_isindexclean         || printf '^'
  zgit_hasunmerged          && printf 'Y'
  zgit_has-assume-unchanged && printf '%b!%b' "$(tput blink)" "$(tput sgr0)"
}

# vim: set ft=zsh ts=2 sw=2 et:
