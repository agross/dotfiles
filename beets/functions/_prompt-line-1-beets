# vim: set ft=zsh ts=2 sw=2 et:
# shellcheck disable=SC2168,SC2086

(($+commands[beet])) || return

[[ $1 == --status ]] || return

music=(*.(mp3|flac|mpc)(.NY1))
if ((#music == 0)); then
  return
fi

[[ $PWD == */_Singles* ]] && return

local album_info cover_info album cover embedded_cover

album_info=("${(@f)$(beet info --include-keys=album_id,artpath \
                               --library \
                               --summarize \
                               path:${PWD})}")

album_id=${album_info[1]#album_id: }
album="%B%F{blue}id:${album_id:-?}%f%b"

printf 'ðŸ’¿ %s' $album

cover=$album_info[2]
if [[ -n $cover ]]; then
  if [[ $cover == *artpath:\ None ]]; then
    cover='%B%F{red}âœ˜%f%b ðŸ–¼'
  else
    cover='%B%F{green}âœ”%f%b ðŸ–¼'
  fi

  printf ', %s' $cover
fi

if [[ ! -v PROMPT_BEET_NO_COVER && -n $album_id ]]; then
  cover_info=$(beet info --include-keys=art \
                         --summarize \
                         path:${PWD})
  if [[ $cover_info == 'art: True' ]]; then
    embedded_cover='%B%F{green}âœ”%f%b ðŸ“„'
  else
    embedded_cover='%B%F{red}âœ˜%f%b ðŸ“„'
  fi

  printf ', %s' $embedded_cover
fi
