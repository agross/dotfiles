autoload -U colors && colors

local dictionaries="${${(%):-%x}:h:h}/dictionaries"

local dict="$dictionaries/de"
if [[ "$1" =~ ^- ]]; then
  dict="$dictionaries/${1#-}"
  shift
fi

if [[ ! -f "$dict" ]]; then
  >&2 echo Dictionary $dict not found.

  # Glob qualifiers:
  #  .  = plain files only
  #  on = sort files by name
  #  N  = no matches are OK
  #  ^D = do not match files starting with dot
  #  :t = print file name only
  local available="$(print $dictionaries/*(.onN^D:t))"
  >&2 echo -e Available dictionaries \(enable with -\<name\>\): ${fg_bold[green]}${available}${reset_color}
  return 1
fi

local word="$@"

zmodload zsh/mapfile

local lines
lines=(${(f)mapfile[$dict]})

typeset -A dictionary
local line
for line in $lines; do
  local kv=("${(@s/ /)line}")
  dictionary[${kv[1]:l}]=$kv[2]
done

echo -e "Spelling: $fg_bold[green]$word$reset_color\n"

local char
for char in ${(s::)word}; do
  echo -e $fg_bold[green]$char$reset_color  $fg_bold[blue]$dictionary[$char:l]$reset_color
done

# vim:set filetype=sh:
