#!/usr/bin/env zsh

set -euo pipe_fail

autoload -Uz colors && colors

root=~/GROSSWEBER/Rechnungswesen/Finanzbuchhaltung/offen/AG*
list=_files.csv

rm --verbose --force -- $~root/$list(.N)

lists_written=()

for file in $~root/*.pdf(.N); do
  dirname=${file:h}
  basename=${file:r:t}

  supplier=${basename% *}
  number=${basename##* }

  printf '%s;%s;%s;;%s\n' ${supplier:0:20} $number brutto 19 >> $dirname/$list
  lists_written+=($dirname/$list)

  printf '%b%s%b: %b%s%b %b%s%b\n' \
         $fg_bold[blue] \
         ${file:t2} \
         $reset_color\
         $fg_bold[green] \
         $supplier \
         $reset_color \
         $fg_bold[cyan] \
         $number \
         $reset_color
done

${VISUAL:-${EDITOR:-vi}} -- ${(u)lists_written}
