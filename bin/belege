#!/usr/bin/env zsh

set -euo pipe_fail

autoload -Uz colors && colors

root=~/GROSSWEBER/Rechnungswesen/Finanzbuchhaltung/offen/AG*
list=_files.csv

rm --verbose --force -- $~root/$list(.N)

lists_written=()

for file in $~root/*.pdf(.N); do
  dirname=${file:h}
  basename=${file:r:t}

  supplier=${basename% *}
  number=${basename##* }

  vat=19
  [[ $supplier == Taxi* ]] && vat=7

  csv=$dirname/$list

  [[ ! -f $csv ]] && printf '\xEF\xBB\xBF' > $csv

  printf '%s;"%s";%s;;%s\n' ${supplier:0:20} $number brutto $vat >> $csv
  lists_written+=($csv)

  printf '%b%s%b: %b%s%b %b%s%b %b%s%%%b\n' \
         $fg_bold[blue] \
         ${file:t2} \
         $reset_color\
         $fg_bold[green] \
         $supplier \
         $reset_color \
         $fg_bold[cyan] \
         $number \
         $reset_color \
         $fg_bold[magenta] \
         $vat \
         $reset_color
done

if (( $#lists_written )); then
  ${VISUAL:-${EDITOR:-vi}} -- ${(u)lists_written}
fi
