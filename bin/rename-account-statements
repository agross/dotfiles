#!/usr/bin/env zsh

# Rename account statement PDFs as per my conventions. Perform dry run with -n.

set -eu
setopt PIPE_FAIL

opt=(-Q -v $@)

autoload -U zmv

# DKB.
#
# Kontoauszug_12345678_Nr_2018_001_per_2018_02_01.pdf
# ->
# Kontoauszug 12345678 2018-02-01.pdf
zmv $opt \
    '(Kontoauszug)_([[:digit:]]##)_*_per_([[:digit:]]##)_([[:digit:]]##)_([[:digit:]]##).pdf(.N)' \
    '$1 $2 $3-$4-$5.pdf'

# DKB VISA.
#
# Kreditkartenabrechnung_1234xxxxxxxx5678_per_2018_02_01.pdf
# or
# Kreditkartenabrechnung_1234________5678_per_2018_02_01.pdf
# ->
# Kreditkarte 5678 2018-02-01.pdf
zmv $opt \
    '(Kreditkarte)nabrechnung_*[_x]([[:digit:]]##)_per_([[:digit:]]##)_([[:digit:]]##)_([[:digit:]]##).pdf(.N)' \
    '$1 $2 $3-$4-$5.pdf'

# netbank.
#
# 0001234567_Kontoauszug_Nr_00001_vom_01-02-2018_985200185765501000.pdf
# ->
# Kontoauszug 1234567 2018-02-28.pdf
zmv $opt \
    '([[:digit:]]##)_(Kontoauszug)_*_vom_([[:digit:]]##)-([[:digit:]]##)-([[:digit:]]##)_[[:digit:]]##.pdf(.N)' \
    '$2 ${1##0##} $5-$4-$3.pdf'

# Renault Bank.
#
# 187730200_2020_Nr.009_Kontoauszug_vom_30.09.2020_20210130091531.pdf
# ->
# Kontoauszug 187730200 2020-09-30.pdf
zmv $opt \
    '([[:digit:]]##)_*_Kontoauszug_vom_([[:digit:]](#c2)).([[:digit:]](#c2)).([[:digit:]](#c4))_*.pdf(.N)' \
    'Kontoauszug $1 $4-$3-$2.pdf'

# 1877302_2021_Jahres-Steuerbescheinigung 2020_vom_29.01.2021_20210130091521.pdf
# ->
# Steuerbescheinigung 2020.pdf
zmv $opt \
    '([[:digit:]]##)_*_Jahres-Steuerbescheinigung ([[:digit:]](#c4))_*.pdf(.N)' \
    'Steuerbescheinigung $2.pdf'

# LBB/Amazon.
#
# Kartenabrechnung_13082020_4920.pdf or
# Kreditkartenabrechnung_13082020_4920.pdf
# ->
# Amazon 2020-08-13.pdf
zmv $opt \
    '(Kartenabrechnung|Kreditkartenabrechnung)_([[:digit:]](#c2))([[:digit:]](#c2))([[:digit:]](#c4))_*.pdf(.N)' \
    'Amazon $4-$3-$2.pdf'
