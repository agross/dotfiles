#!/usr/bin/env zsh

# Rename account statement PDFs as per my conventions. Perform dry run with -n.

set -eu
setopt PIPE_FAIL

opt=(-Q -v $@)

autoload -U zmv

# DKB.
#
# 2023-09-08_Kontoauszug_19_2023_vom_05.09.2023_zu_Konto_12345678.pdf
# ->
# Kontoauszug 12345678 2023-09-05.pdf
zmv $opt \
    '[[:digit:]](#c4)-[[:digit:]](#c2)-[[:digit:]](#c2)_(Kontoauszug)_*_vom_([[:digit:]](#c2)).([[:digit:]](#c2)).([[:digit:]](#c4))_zu_Konto_([[:digit:]]##).pdf(.N)' \
    '$1 $5 $4-$3-$2.pdf'

# DKB VISA.
#
# 2024-11-18_Kreditkartenabrechnung_vom_22._Oktober_2024.pdf
# ->
# Kreditkarte 2024-10-22.pdf
declare months=(
  Januar
  Februar
  MÃ¤rz
  April
  Mai
  Juni
  Juli
  August
  September
  Oktober
  November
  Dezember
)

for ((month = 1; month <= $#months; month++)); do
  zmv $opt \
      '*_(Kreditkarte)nabrechnung_vom_([[:digit:]](#c2))._'$months[$month]'_([[:digit:]](#c4)).pdf(.N)' \
      '$1 $3-'${(l:2::0:)month}'-$2.pdf'
done

# Renault Bank.
#
# 187730200_2020_Nr.009_Kontoauszug_vom_30.09.2020_20210130091531.pdf
# ->
# Kontoauszug 187730200 2020-09-30.pdf
zmv $opt \
    '([[:digit:]]##)_*_Kontoauszug_vom_([[:digit:]](#c4)).([[:digit:]](#c2)).([[:digit:]](#c2))_*.pdf(.N)' \
    'Kontoauszug $1 $2-$3-$4.pdf'

# 1877302_2021_Jahres-Steuerbescheinigung 2020_vom_29.01.2021_20210130091521.pdf
# ->
# Steuerbescheinigung 2020.pdf
zmv $opt \
    '([[:digit:]]##)_*_Jahres-Steuerbescheinigung ([[:digit:]](#c4))_*.pdf(.N)' \
    'Steuerbescheinigung $2.pdf'
